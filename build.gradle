import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'war'
    id 'distribution'
}

group 'org.citydb.wfs'
version '5.2.0'

apply from: 'properties.gradle'

ext {
    genSrcDir = 'src-gen/main/java'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
    compileJava {
        options.release = 8
    }
    sourceSets {
        main.java.srcDir genSrcDir
    }
}

configurations {
    jdbc
    jaxb
}

repositories {
    maven {
        url 'https://repo.osgeo.org/repository/release'
    }
    maven {
        url 'https://citydb.jfrog.io/artifactory/maven'
    }
    mavenCentral()
}

dependencies {
    implementation 'org.citydb:impexp-core:5.2.0'
    implementation 'com.github.seancfoley:ipaddress:5.3.4'
    implementation 'org.glassfish.jersey.containers:jersey-container-servlet:2.35'
    implementation 'org.glassfish.jersey.inject:jersey-hk2:2.35'

    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'

    jaxb 'org.glassfish.jaxb:jaxb-xjc:2.3.2'
    jaxb 'org.jvnet.jaxb2_commons:jaxb2-basics:0.12.0'
    jaxb 'org.jvnet.jaxb2_commons:jaxb2-namespace-prefix:1.3'
    jaxb 'org.jvnet.jaxb2_commons:jaxb2-basics-annotate:1.1.0'
    jaxb 'org.slf4j:slf4j-simple:1.7.25'
}

task processLicense(type: Copy) {
    from("resources/license/LICENSE.txt") {
        filteringCharset = 'UTF-8'
        filter(ReplaceTokens, tokens: [
                name: project.citydbWfsName,
                vendorName: project.vendorName,
                vendorStreet: project.vendorStreet,
                vendorTown: project.vendorTown,
                vendorCountry: project.vendorCountry,
                vendorCopyright: project.vendorCopyright,
                vendorWebsiteUrl: project.vendorWebsiteUrl
        ])
    }
    into "$buildDir/tmp/license"
}

processResources {
    filteringCharset = 'ISO-8859-1'
    filesMatching('**/application.properties') {
        filter(ReplaceTokens, tokens: [
                name: project.citydbWfsName,
                version: project.version,
                vendorName: project.vendorName,
                vendorStreet: project.vendorStreet,
                vendorTown: project.vendorTown,
                vendorCountry: project.vendorCountry,
                vendorCopyright: project.vendorCopyright,
                vendorWebsiteUrl: project.vendorWebsiteUrl
        ])
    }
}

task generateJaxb(group: 'jaxb') {
    outputs.dir genSrcDir

    doLast {
        mkdir genSrcDir

        System.setProperty('javax.xml.accessExternalSchema', 'file')
        ant.taskdef(name: 'xjc', classname: 'com.sun.tools.xjc.XJC2Task', classpath: configurations.jaxb.asPath)
        ant.xjc(destdir: genSrcDir,
                schema: 'resources/jaxb/wfs-jaxb.xsd',
                binding: 'resources/jaxb/binding.xjb',
                extension: 'true',
                encoding: 'UTF-8') {
            produces(dir: genSrcDir, includes: '**/*.java')
            arg(line: '-Xnamespace-prefix -Xsetters -Xsetters-mode=direct -Xinheritance -Xannotate -XremoveAnnotation')
        }

        delete "$genSrcDir/net/opengis/citygml"
        delete "$genSrcDir/net/opengis/gml"
        delete "$genSrcDir/org"
        delete "$genSrcDir/oasis"
    }
}

task cleanJaxb(type: Delete, group: 'jaxb') {
    delete 'src-gen'
}

distributions.main {
    distributionBaseName = project.appName
    contents {
        from war
        from 'CHANGES.md'
        from 'README.md'
        into('license') {
            from "$rootDir/resources/license/APACHE-2.0.txt"
            from processLicense
        }
    }
}

war {
    archiveFileName = project.name + '.war'
    manifest {
        attributes('Implementation-Title': project.appName,
                'Implementation-Version': project.version,
                'Implementation-Vendor': project.vendorName + ', ' + project.vendorWebsiteUrl
        )
    }
    into('META-INF') {
        from processLicense
    }
}